apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.github.spullara.mustache.java:compiler:0.8.10'
	compile 'org.pegdown:pegdown:1.2.1'
	compile 'org.eclipse.jgit:org.eclipse.jgit:2.2.0.201212191850-r'
	compile 'commons-io:commons-io:2.4'
	compile 'org.reflections:reflections:0.9.8'
	compile 'com.rubiconproject.oss:jchronic:0.2.6'
	compile 'log4j:log4j:1.2.17'
}

jar {
    // Include all the dependencies to create a fat jar
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes("Main-Class": "org.denevell.rocklobster.Main" )
    }
    baseName = 'RockLobster'
}

// Used to collect all the jars if needed. 
// They're in the .classpath when you run gradle eclipse anyway, and the jar is a fat one with all the dependencies
task copyToLib(type: Copy) {
    from configurations.compile
    into "${buildDir}/libs"
}

task runJar(dependsOn:jar) << {
  javaexec { main="-jar"; args jar.archivePath } 
}

task zip(type: Zip) {
    archiveName = "PackagedRockLobster.zip"
    from projectDir
    include 'resources/**/*'
    include 'README.md'
    include 'rock.lobster'
    include '*.template.*'
    include 'build/libs/RockLobster.jar'
}
